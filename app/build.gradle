import java.text.SimpleDateFormat

apply plugin: 'com.android.application'

def generateVersionCode() {
    def formatter = new SimpleDateFormat("yyyyMMddHH")
    def now = new Date()
    return Integer.parseInt(formatter.format(now))
}

def generateVersionName() {
    def formatter = new SimpleDateFormat("yyyy.MM.dd")
    def now = new Date()
    return formatter.format(now)
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    defaultConfig {
        applicationId "com.kevinmacwhinnie.fonz"
        minSdkVersion 19
        targetSdkVersion 23
        versionCode generateVersionCode()
        versionName generateVersionName()
    }
    signingConfigs {
        def propertiesFile = project.rootProject.file('signing.properties')
        if (!propertiesFile.exists()) {
            logger.warn('No signing.properties file, using default keystore')
        } else {
            def properties = new Properties()
            properties.load((InputStream) propertiesFile.newDataInputStream())

            def keyStoreHome = properties.getProperty('keyStoreHome')
            debug {
                storeFile new File(keyStoreHome, properties.getProperty("debugKeyStoreName"))
                storePassword properties.getProperty("debugKeyStorePassword")
                keyAlias properties.getProperty("debugKeyStoreAlias")
                keyPassword properties.getProperty("debugKeyStorePassword")
            }
            release {
                storeFile new File(keyStoreHome, properties.getProperty("releaseKeyStoreName"))
                storePassword properties.getProperty("releaseKeyStorePassword")
                keyAlias properties.getProperty("releaseKeyStoreAlias")
                keyPassword properties.getProperty("releaseKeyStorePassword")
            }
        }
    }
    lintOptions {
        disable 'InvalidPackage'
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:23.1.0'
    compile 'com.android.support:recyclerview-v7:23.1.0'

    provided 'com.squareup.dagger:dagger-compiler:1.2.2'
    compile 'com.squareup.dagger:dagger:1.2.2'

    compile 'com.squareup:otto:1.3.8'
    compile 'com.jakewharton:butterknife:7.0.1'

    compile 'is.hello:anime-android-go-99:0.4.0'

    testCompile 'org.robolectric:robolectric:3.0'
    testCompile 'junit:junit:4.12'
}
